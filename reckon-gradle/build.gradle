plugins {
  id 'groovy'
  id 'java-library'
  id 'java-gradle-plugin'
}

sourceCompatibility = 8

repositories {
  jcenter()
}

dependencies {
  // gradle
  api gradleApi()

  // reckon
  api project(':reckon-core')

  // git
  implementation 'org.eclipse.jgit:org.eclipse.jgit:latest.release'
  implementation 'org.ajoberstar:grgit:latest.release'
  compatTestImplementation 'org.ajoberstar:grgit:latest.release'

  // util
  implementation 'com.google.guava:guava:[21.0,)'
  implementation 'com.github.zafarkhaja:java-semver:[0.9.0,)'

  // testing
  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.1-groovy-2.4'
  compatTestImplementation 'org.codehaus.groovy:groovy-all:2.4.15'
  compatTestImplementation 'junit:junit:4.12'
}

stutter {
  sparse = true
  includeActiveRc = true

  java(8) {
    compatibleRange '3.0'
  }
  java(9) {
    compatibleRange '4.2.1'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/reckon'
  vcsUrl = 'https://github.com/ajoberstar/reckon.git'
  description = 'Infer a project\'s version from your Git repository'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.reckon'
      displayName = 'Reckon Plugin'
      tags = ['semver', 'git', 'version', 'versioning']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
