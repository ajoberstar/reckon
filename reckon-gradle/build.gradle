plugins {
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'

  id 'com.gradle.plugin-publish'
  id 'org.ajoberstar.stutter'
}

tasks.withType(JavaCompile) {
  options.release = 8
}

dependencies {
  // gradle
  compileOnly gradleApi()

  // reckon
  api project(':reckon-core')

  // git
  api "org.ajoberstar.grgit:grgit-gradle:$grgitVersion"
  implementation "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
  compatTestImplementation "org.ajoberstar.grgit:grgit-core:$grgitVersion"

  // util
  implementation 'com.google.guava:guava:latest.release'

  // testing
  compatTestImplementation gradleTestKit()
  compatTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
  compatTestImplementation 'org.codehaus.groovy:groovy-all:[2.5,2.6-alpha)'
  compatTestImplementation 'junit:junit:latest.release'
}

stutter {
  sparse = true

  java(8) {
    compatibleRange '4.0'
  }
  java(11) {
    compatibleRange '5.0'
  }
  java(15) {
    compatibleRange '6.3'
  }
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'org.ajoberstar.reckon.gradle'
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/reckon'
  vcsUrl = 'https://github.com/ajoberstar/reckon.git'
  description = 'Infer a project\'s version from your Git repository'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.reckon'
      displayName = 'Reckon Plugin'
      tags = ['semver', 'git', 'version', 'versioning']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}

// remove duplicate publication
gradlePlugin.automatedPublishing = false
